

!!==============================================================================
!!
!!	EL CHICO
!!	Personalizaciones de la librería Inform
!!
!!==============================================================================
!!
!!	File:			chico_lib.inf
!!	Author(s):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Language:		ES (Castellano)
!!	System:			Inform-INFSP 6
!!	Platform:		Máquina-Z / GLULX
!!	Version:		2.0
!!	Release:		2017/08/25
!!
!!------------------------------------------------------------------------------
!!
!!	# HISTORIAL DE VERSIONES
!!
!!	2.0: 2017/08/25	Integración de la extensión 'SpanishLM.h'.
!!	1.3: 2016/03/26	Las funciones para los determinantes demostrativos
!!					ahora también imprimen el nombre del objeto además del
!!					determinante adecuado.
!!	1.2: 2015/02/11	Añadidas funciones complementarias para los determinantes
!!					demostrativos: _Este(), _Ese() y _Aquel().
!!	1.1: 2015/02/07 Añadidas funciones _este(), _ese(), _aquel() para imprimir
!!					los determinantes demostrativos adecuados para un obj.
!!	1.0: 2015/01/29	Versión original.
!!
!!------------------------------------------------------------------------------
!!
!!	This program is free software; you can redistribute it and/or modify
!!	it under the terms of the GNU General Public License as published by
!!	the Free Software Foundation; either version 2 of the License, or
!!	(at your option) any later version.
!!
!!	This program is distributed in the hope that it will be useful,
!!	but WITHOUT ANY WARRANTY; without even the implied warranty of
!!	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!	GNU General Public License for more details.
!!
!!	You should have received a copy of the GNU General Public License along
!!	with this program; if not, write to the Free Software Foundation, Inc.,
!!	51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA. Or see
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (C) 2017, J. Francisco Martín
!!
!!------------------------------------------------------------------------------
!!
!!	# TABLA DE CONTENIDOS
!!
!!	-	Banner()
!!		-	PrintStoryTitle()
!!		-	PrintHeadline()
!!		-	PrintVersionNumber()
!!		-	PrintSerialNumber()
!!	-	ExamineSub()
!!	-	GetGNAOfObject()
!!	-	GetActionVerb()
!!	-	LMode1Sub()
!!	-	LMode2Sub()
!!	-	LMode3Sub()
!!	-	LookSub()
!!	-	ParseNoun()
!!	-	PSN__()
!!	-	SpanishNominativePronoun()
!!	-	VersionSub()
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!! Trata de transferir un objeto dado al jugador: retorna falso si la acción se
!! realiza con éxito, verdadero si se produce algún error (imprimiendo un
!! mensaje adecuado en este último caso).
!!
!!	@override verblibm.h
!!------------------------------------------------------------------------------
!! [ AttemptToTakeObject item     ancestor after_recipient i j k;
!! 	if (item == player) return L__M(##Take, 2);
!! 	if (item has animate) return L__M(##Take, 3, item);
!!
!! 	ancestor = CommonAncestor(player, item);
!!
!! 	if (ancestor == 0) {
!! 		i = ObjectScopedBySomething(item);
!! 		if (i ~= 0) ancestor = CommonAncestor(player, i);
!! 	}
!!
!! 	!! Is the player indirectly inside the item?
!! 	if (ancestor == item) return L__M(##Take, 4, item);
!!
!! 	!! Does the player already directly contain the item?
!! 	if (item in player) return L__M(##Take, 5, item);
!!
!! 	!! Can the player touch the item, or is there (e.g.) a closed container
!! 	!! in the way?
!! 	if (ObjectIsUntouchable(item, false, true)) return;
!!
!! 	!! The item is now known to be accessible.
!!
!! 	!! Consult the immediate possessor of the item, if it's in a container
!! 	!! which the player is not in.
!!
!! 	i = parent(item);
!! 	if (i && i ~= ancestor && (i has container || i has supporter)) {
!! 		after_recipient = i;
!! 		k = action; action = ##LetGo;
!! 		if (RunRoutines(i, before) ~= 0) { action = k; rtrue; }
!! 		action=k;
!! 	}
!!
!! 	if (item has scenery) return L__M(##Take, 10, item);
!! 	if (item has static)  return L__M(##Take, 11, item);
!!
!! 	!! En este punto se sabe que el objeto está en condiciones de ser cogido.
!! 	!! Se comprueba ahora si el personaje lleva ya demasiados objetos consigo.
!! 	!! Si es así y en caso de que sea posible, se trata de meter algún objeto
!! 	!! en el SACK_OBJECT para hacer sitio en el inventario.
!!
!! 	k = 0; objectloop (j in player) if (j hasnt worn) k++;
!!
!! 	if (k >= ValueOrRun(player, capacity)) {
!! 		if (SACK_OBJECT ~= 0) {
!! 			!! XXX - Para introducir automáticamente algún objeto en el
!! 			!! SACK_OBJECT, se comprueba que éste esté en posesión del
!! 			!! personaje y lo lleve puesto. Esta segunda comprobación se ha
!! 			!! añadido al relato dado que el SACK_OBJECT es la ropa del
!! 			!! personaje (los bolsillos de su abrigo). Si el SACK_OBJECT fuese
!! 			!! una mochila, por ejemplo, la comprobación sería innecesaria.
!! 			if (parent(SACK_OBJECT) ~= player || SACK_OBJECT hasnt worn)
!! 				return L__M(##Take, 12);
!! 			j = 0;
!! 			objectloop (k in player)
!! 				if (k ~= SACK_OBJECT && k hasnt worn && k hasnt light
!! 					!! XXX - Se comprueba además si el objeto cabe dentro o
!! 					!! sobre SACK_OBJECT de forma que, si no lo hace, no se
!! 					!! intenta introducirlo automáticamente. La rutina
!! 					!! CheckIfObjectFits(receiver, obj) está definida dentro de
!! 					!! la extensión "receptacles.h".
!! 					&& CheckIfObjectFits(SACK_OBJECT, k)) j = k;
!!
!! 			if (j ~= 0) {
!! 				L__M(##Take, 13, j);
!! 				new_line;
!! 				keep_silent = 1; <Insert j SACK_OBJECT>; keep_silent = 0;
!! 				if (j notin SACK_OBJECT) rtrue;
!! 			}
!! 			else return L__M(##Take, 12);
!! 		}
!! 		else return L__M(##Take, 12);
!! 	}
!!
!! 	!! Transfer the item.
!! 	move item to player;
!!
!! 	!! Send "after" message to the object letting go of the item, if any.
!! 	if (after_recipient ~= 0) {
!! 		k = action; action = ##LetGo;
!! 		if (RunRoutines(after_recipient, after) ~= 0) { action = k; rtrue; }
!! 		action=k;
!! 	}
!! 	rfalse;
!! ];


!!==============================================================================
!! Conjunto de rutinas para imprimir los créditos de la aplicación en
!! castellano. Lista de rutinas incluidas: Banner(), PrintHeadline(),
!! PrintVersionNumber() y PrintSerialNumber().
!!
!!	@override verblibm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
[ Banner;
	PrintStoryTitle();
	PrintHeadline();
	#Ifdef DEBUG;
	print "ALFA ";
	#Ifnot;
	print "Versión ";
	#Endif; ! DEBUG;
	PrintVersionNumber();
	print " / Núm. de serie ";
	PrintSerialNumber();
	print " / Creado con Inform "; inversion;
	#Ifdef TARGET_ZCODE;
	print " Z";
	#Endif; ! TARGET_ZCODE;
	#Ifdef DEBUG;
	print " ";
	#Endif; ! DEBUG;
	#Ifdef STRICT_MODE;
	print "S";
	#Endif; ! STRICT_MODE
	#Ifdef INFIX;
	print "X";
	#Endif; ! INFIX
	new_line;
];

!! Imprime el título
[ PrintStoryTitle;
	if (Story ~= 0) {
		#Ifdef TARGET_ZCODE;
		#IfV5; style bold; #Endif;
		print (string) Story;
		#IfV5; style roman; #Endif;
		#Ifnot; ! TARGET_GLULX;
		glk($0086, 3); ! set header style
		print (string) Story;
		glk($0086, 0); ! set normal style
		#Endif; ! TARGET_
		new_line;
	}
];

!! Imprime el encabezado
[ PrintHeadline;
	if (Headline ~= 0) {
		print (string) Headline;
		new_line;
	}
];

!! Imprime el número de versión
[ PrintVersionNumber
	i;
	i = 0;  ! shut up compiler warning
	print Checking, ".";
	#Ifdef	TARGET_ZCODE;
	print (HDR_GAMERELEASE-->0) & $03ff;
	#Ifnot;	! TARGET_GLULX;
	@aloads ROM_GAMERELEASE 0 i;
	print i;
	#Endif;	! TARGET_
];

!! Imprime el número de serie
[ PrintSerialNumber
	i;
	#Ifdef	TARGET_ZCODE;
	for (i=0 : i<6 : i++) print (char) HDR_GAMESERIAL->i;
	#Ifnot;	! TARGET_GLULX;
	for (i=0 : i<6 : i++) print (char) ROM_GAMESERIAL->i;
	#Endif;	! TARGET_
];

!!==============================================================================
!! Dibuja una barra de estado con el nombre de la localidad centrado. (NOTA:
!! La barra de estado utilizada finalmente en el relato es en realidad una
!! extensión de GWindows y no la barra de estado definida aquí, de manera que
!! esta rutina queda inutilizada).
!!
!!	@override parserm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
!! Buffer auxiliar para calcular la longitud de un texto:
#Ifdef	VN_1630;	! compilador 6.30 o superior
Array _AlmacenDeTexto_ buffer 160; ! 150 y algo caracteres deberían bastar
#Ifnot;				! compiladores más viejos
Array	_AlmacenDeTexto_ -> 160 + WORDSIZE;
#Endif;	!VN_1630

[ DrawStatusLine
	width pos;
	#Ifdef TARGET_GLULX;
	! If we have no status window, we must not try to redraw it.
	if (gg_statuswin == 0) return;
	#Endif; ! TARGET_GLULX
	! If there is no player location, we should not try either.
	if (location == nothing || parent(player) == nothing) return;

	StatusLineHeight(gg_statuswin_size);
	MoveCursor(1, 1);
	width = ScreenWidth();
	spaces width;

	!! Primero calculamos la posición inicial para imprimir centrado.
	!! XXX - Si se cambia el nombre o el tamaño del buffer de texto temporal,
	!! se deben cambiar también los dos primeros argumentos de PrintToBuffer().
	pos = (width - PrintToBuffer(_AlmacenDeTexto_, 160, PrintLocationName)) / 2;
	MoveCursor(1, pos); 	! se mueve el cursor a la posición calculada
	PrintLocationName();	! se imprime el nombre de la localidad

	MainWindow();
];

!! Imprime el nombre de la localidad en que se encuentra el personaje
!! controlado por el usuario.
[ PrintLocationName;
	if (location == thedark) print (name) location;
	else {
		FindVisibilityLevels();
		if (visibility_ceiling == location) print (name) location;
		else print (The) visibility_ceiling;
	}
];


!!==============================================================================
!! Rutina para obtener la descripción de un objeto. Es idéntica a la rutina
!! original implementada en 'verblibm.h' salvo por un detalle: al examinar un
!! objeto por primera vez se le asigna el nuevo atributo 'described'.
!!
!!	@returns {boolean}
!!	@override verblibm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
Attribute described; ! Indica si un objeto ha sido descrito al jugador
[ ExamineSub
	i;
	if (location == thedark) return L__M(##Examine, 1);
	i = noun.description;
	if (i == 0) {
		if (noun hasnt described) give noun described;
		if (noun has container) <<Search noun>>;
		if (noun has switchable) { L__M(##Examine, 3, noun); rfalse; }
		return L__M(##Examine, 2, noun);
	}
	PrintOrRun(noun, description);
	if (noun has switchable) L__M(##Examine, 3, noun);
	if (noun hasnt described) give noun described;
	if (AfterRoutines() == 1) rtrue;
];


!!==============================================================================
!! Determina el código GNA ('gender-number-animation') del nombre corto del
!! objeto 'obj' pasado como parámetro. El código GNA es un número entero entre
!! 0 y 11:
!!
!!		0	animate		singular	masculine
!!		1							feminine
!!		2							neuter
!!		3				plural		masculine
!!		4							feminine
!!		5							neuter
!!		6	inanimate	singular	masculine
!!		7							feminine
!!		8							neuter
!!		9				plural		masculine
!!		10							feminine
!!		11							neuter
!!
!! Para determinar el código GNA de un objeto en INFSP6 se pueden seguir dos
!! estrategias: 1) utilizar sus atributos 'animate', 'male', 'female', 'neuter'
!! y 'pluralname' o, 2) si el objeto tiene definida una propiedad 'gender',
!! utlizando esta última. La rutina original hace uso únicamente de esa primera
!! estrategia, pero presenta el inconveniente de que el resultado no es siempre
!! el mismo para un objeto dado, puesto que la librería modifica sus atributos
!! automáticamente en tiempo de ejecución. La segunda estrategia, por su parte,
!! arroja siempre el mismo resultado. Esta implementación de la rutina incluye
!! un parámetro opcional booleano que permite seleccionar el tipo de estrategia
!! utilizado para determinar el GNA: el valor falso por defecto para usar la
!! estrategia 1 de la rutina original; verdadero para usar la estrategia 2.
!!
!!	@param {Object} obj - Objeto del que se desea conocer su código GNA
!!	@param {boolean} [flag=false] - Si se invoca con este parámetro activado
!!		(==true), el GNA se calcula utilizando el atributo 'animate' y la
!!		propiedad 'gender' del objeto. En caso contrario lo hace basándose en
!!		sus atributos 'animate', 'male', 'female', 'neuter' y 'pluralname'
!!	@returns {integer} Código GNA del objeto [0-11]
!!	@override parserm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
[ GetGNAOfObject obj flag
	result gen pl;
    if (obj hasnt animate) result = 6;
	if (flag && obj provides gender) {
		switch (obj.gender) {
			1: gen = male;
			2: gen = female;
			3: gen = male; pl = true;
			4: gen = female; pl = true;
		}
	} else {
    	if (obj has male) gen = male;
	    if (obj has female) gen = female;
	    if (obj has neuter) gen = neuter;
	}
    if (gen == 0) {
        if (result == 0) gen = LanguageAnimateGender;
        else gen = LanguageInanimateGender;
    }
    if (gen == female) result = result + 1;
    if (gen == neuter) result = result + 2;
	if (pl) result = result + 3;
	else if (obj has pluralname) result = result + 3;

    return result;
];


!!==============================================================================
!! Retorna el verbo que se corresponde a la acción dada como parámetro.
!!
!!	@param {Object} x - Constante con una acción
!!	@returns {string} Una palabra que se corresponde con la acción 'x'
!!	@version 1.0
!!------------------------------------------------------------------------------
[ GetActionVerb x;
	switch (x) {
		!! GROUP 2 ACTIONS: Usually work, given the right circumstances.
		##Close:		return "Cerrar";
		##Disrobe:		return "Desvestir";
		##Drop:			return "Dejar";
		##Eat:			return "Comer";
		##Empty:		return "Vaciar";
		!##EmptyT
		##Enter:		return "Entrar";
		##Examine:		return "Examinar";
		##Exit:			return "Salir";
		##GetOff:		return "Salir";
		##Give:			return "Dar";
		##Go:			return "Ir";
		##GoIn:			return "Entrar";
		##Insert:		return "Meter";
		##Inv:			return "Inventario";
		##InvTall:		return "Inventario (lista)";
		##InvWide:		return "Inventario (descripción)";
		##Lock:			return "Bloquear";
		##Look:			return "Mirar";
		##Open:			return "Abrir";
		##Order:		return "Ordenar";
		##PutOn:		return "Poner encima";
		##Remove:		return "Quitar";
		##Search:		return "Buscar";
		##Show:			return "Enseñar";
		##SwitchOff:	return "Apagar";
		##SwitchOn:		return "Encender";
		##Take:			return "Coger";
		!##Transfer
		##Unlock:		return "Desbloquear";
		!##VagueGo
		##Wear:			return "Vestir";
		!! GROUP 3 ACTIONS: Output a message and stop at the "before" stage.
		##Answer:		return "Responder";
		##Ask:			return "Preguntar";
		##AskFor:		return "Preguntar por";
		##Attack:		return "Atacar";
		##Blow:			return "Soplar";
		##Burn:			return "Quemar";
		##Buy:			return "Comprar";
		##Climb:		return "Escalar";
		##Consult:		return "Consultar";
		##Cut:			return "Cortar";
		##Dig:			return "Excavar";
		##Drink:		return "Beber";
		##Fill:			return "Llenar";
		##Jump:			return "Saltar";
		##JumpOver:		return "Saltar";
		##Kiss:			return "Besar";
		!##LetGo:
		##Listen:		return "Escuchar";
		##LookUnder:	return "Mirar debajo";
		##Mild:			return "Insultar";
		##No:			return "No";
		##Pray:			return "Rezar";
		##Pull:			return "Tirar";
		##Push:			return "Empujar";
		##PushDir:		return "Empujar hacia";
		##Receive:		return "Recibir";
		##Rub:			return "Frotar";
		##Set:			return "Establecer";
		##SetTo:		return "Establecer a";
		##Sing:			return "Cantar";
		##Sleep:		return "Dormir";
		##Smell:		return "Oler";
		##Sorry:		return "Disculpar";
		##Squeeze:		return "Agitar";
		##Strong:		return "Blasfemar";
		##Swim:			return "Nadar";
		##Swing:		return "Columpiar";
		##Taste:		return "Saborear";
		##Tell:			return "Decir";
		##Think:		return "Pensar";
		##ThrowAt:		return "Lanzar a";
		##Tie:			return "Atar";
		##Touch:		return "Tocar";
		##Turn:			return "Girar";
		##Wait:			return "Esperar";
		##Wake:			return "Despertar";
		##WakeOther:	return "Despertar";
		##Wave:			return "Gesticular";
		##WaveHands:	return "Gesticular con las manos";
		##Yes:			return "Si";
		!! GRAMÁTICA ESPAÑOLA EXTENDIDA:
		##Dance:		return "Bailar";
		##GoDown:		return "Descender";
		##GoUp:			return "Ascender";
		##KnockOn:		return "Llamar";
		##Masturbate:	return "Masturbarse";
		##Read:			return "Leer";
		##Shout:		return "Gritar";
		##Sit:			return "Sentarse";
		##SleepWith:	return "Follar";
		##StandUp:		return "Levantarse";
		##Untie:		return "Desatar";
		##Use:			return "Usar";
		##Write:		return "Escribir";
		##Xyzzy:		return "Xyzzy";
		!! CUSTOM ACTIONS:
		##TalkTo:		return "Hablar";
	}
];


!!==============================================================================
!! Rutinas para tomar inventario del protagonista.
!!
!!	@override verblibm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
!! [ InvSub item number_worn number_carried first_worn;
!!
!! 	!! El protagonista no lleva ningún objeto en el inventario
!! 	if (child(player) == 0) return L__M(##Inv, 1);
!!
!! 	objectloop (item in player) {
!! 		if (item has worn) {
!! 			give item workflag;
!! 			number_worn++;
!! 			if (number_worn == 1) first_worn = item;
!! 		} else {
!! 			give item ~workflag;
!! 			number_carried++;
!! 		}
!! 	}
!!
!! 	!! XXX - La rutina ParseNoun (tanto la rutina original de SpanishG.h como
!! 	!! la versión utilizada aquí) puede modificar temporalmente los atributos
!! 	!! female y pluralname de un objeto en función del nombre que haya
!! 	!! utilizado el interlector para referirse a él, con lo que su GNA cambia.
!! 	!! Esto podía provocar un resultado inadecuado en la primera línea del
!! 	!! inventario (imprimir cosas como "Lleva puesto su ropa..."). Para
!! 	!! corregirlo se han añadido la rutina _o() y una pequeña modificación de
!! 	!! GetGNAOfObject().
!!
!! 	!! Objetos que el protagonista lleva puestos
!! 	if (number_worn ~= 0) {
!! 		print "Llev", (lm_as) player, " puest", (_o) first_worn, " ";
!!
!! 		!! XXX Apuntar que no se toma inventario de los objetos que pueda
!! 		!! haber dentro de aquellos que el protagonista lleva puestos.
!! 		WriteListFrom(child(player), ENGLISH_BIT + WORKFLAG_BIT);
!!
!! 		!! El protagonista tiene cosas en las manos además
!! 		if (number_carried ~= 0) {
!! 			objectloop (item in player) {
!! 				if (item has worn) give item ~workflag;
!! 				else give item workflag;
!! 			}
!! 			print ", y ", (lm_tienes) player, " además ";
!! 			WriteListFrom(child(player),
!! 				ENGLISH_BIT + RECURSE_BIT + FULLINV_BIT + WORKFLAG_BIT);
!! 			print " en las manos";
!! 		}
!!
!! 		if (SACK_OBJECT && SACK_OBJECT in player && SACK_OBJECT has worn) {
!! 			!! Lleva puesto el SACK_OBJECT y NO tiene nada en su interior:
!! 			if (~~child(SACK_OBJECT)) {
!! 				print ". En ", (the) SACK_OBJECT, " no llev", (lm_as) player,
!! 				" nada.";
!! 			}
!! 			!! Lleva puesto el SACK_OBJECT y tiene cosas en su interior:
!! 			else {
!! 				print ". En ", (the) SACK_OBJECT, " guard", (lm_as) player, " ";
!! 				WriteListFrom(child(SACK_OBJECT),
!! 					ENGLISH_BIT + RECURSE_BIT + FULLINV_BIT);
!! 			}
!! 		}
!! 		print ".^";
!! 	}
!!
!! 	!! El protagonista no lleva ningún objeto puesto
!! 	else {
!! 		print (lm_ttienes) player, " ";
!! 		WriteListFrom(child(player), ENGLISH_BIT + RECURSE_BIT + FULLINV_BIT);
!! 		print " en las manos.^";
!! 	}
!!
!! 	#Ifndef MANUAL_PRONOUNS;
!! 	objectloop (x in player) PronounNotice(x);
!! 	#Endif;
!! 	AfterRoutines();
!! ];
!! [ InvTallSub; <Inv>; ];
!! [ InvWideSub; <Inv>; ];


!!==============================================================================
!! Redefinición de los 'lookmode': Breve, Largo y Superbreve.
!!
!!	@override verblibm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
[ LMode1Sub; lookmode = 1; L__M( ##LMode1 ); ];	! Breve
[ LMode2Sub; lookmode = 2; L__M( ##LMode2 ); ];	! Largo (ahora por defecto)
[ LMode3Sub; lookmode = 3; L__M( ##LMode3 ); ];	! Superbreve


!!==============================================================================
!! Redefinición de la rutina 'LookSub' para evitar que se imprima el nombre de
!! la localidad actual antes de su descripción.
!!
!!	@override verblibm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
[ LookSub allow_abbrev  visibility_levels i j k nl_flag;
	if (parent(player) == 0) return RunTimeError(10);

	.MovedByInitial;

	if (location == thedark) { visibility_ceiling = thedark; NoteArrival(); }
	else {
		visibility_levels = FindVisibilityLevels();
		if (visibility_ceiling == location) {
			NoteArrival();
			if (visibility_ceiling ~= location) jump MovedByInitial;
		}
	}

	! Printing the top line: e.g.
	! Octagonal Room (on the table) (as Frodo)
	#Ifdef TARGET_ZCODE;
	new_line;
	style bold;
	if (visibility_levels == 0) print (name) thedark;
	else {
		if (visibility_ceiling ~= location) print (The) visibility_ceiling;
		else print (name) visibility_ceiling;
	}
	for (j=1,i=parent(player) : j<visibility_levels : j++,i=parent(i))
		if (i has supporter) L__M(##Look, 1, i);
		else                 L__M(##Look, 2, i);
	if (print_player_flag == 1) L__M(##Look, 3, player);
	new_line;
	style roman;
	#Endif; ! TARGET_ZCODE;

	! The room description (if visible)
	if (lookmode < 3 && visibility_ceiling == location) {
		if ((allow_abbrev ~= 1) || (lookmode == 2)
			|| (location hasnt visited)) {
			if (location.&describe ~= 0) RunRoutines(location, describe);
			else {
				if (location.description == 0) RunTimeError(11, location);
				else PrintOrRun(location, description);
			}
		}
	}

	if (visibility_ceiling == location) nl_flag = 1;
	if (visibility_levels == 0) Locale(thedark);
	else {
		for (i=player,j=visibility_levels : j>0: j--,i=parent(i))
			give i workflag;
		for (j=visibility_levels : j>0 : j--) {
			for (i=player,k=0 : k<j : k++) i=parent(i);
			if (i.inside_description ~= 0) {
				if (nl_flag) new_line; else nl_flag = 1;
				PrintOrRun(i,inside_description); }
			if (Locale(i)~=0) nl_flag=1;
		}
	}

	LookRoutine();
	ScoreArrival();
	action = ##Look;
	if (AfterRoutines() == 1) rtrue;
];


!!==============================================================================
!! La rutina ParseNoun se ocupa de ignorar "de" cuando aparece entre dos
!! palabras reconocidas como names de un mismo objeto. Así, si un objeto tiene
!! en su campo "name" los valores "caja" "madera" "caoba" ParseNoun aceptará
!! "caja de madera" "caja de caoba" e incluso "caja de madera de caoba". En
!! cambio, ante la cadena "caja de bolsa", sólo aceptará la primera palabra
!! (caja) dejando sin procesar "de bolsa", lo que permite la posterior
!! interpretacion de frases como "saca caja de bolsa" e incluso "saca caja de
!! madera de caoba de bolsa". De paso también ignora artículos con lo que
!! también se admite: "saca la caja de madera de caoba de la bolsa".
!!
!! ParseNoun debe retornar un número que indica cuántas palabras admite como
!! pertenecientes al objeto. En nuestro ejemplo, ante "caja de madera" debe
!! retornar 3, pero ante "caja de bolsa" debe retornar 1.
!!
!! MODIFICACIÓN: Si usamos la rutina original y redefinimos el objeto con el
!! valor "caja" en sus campos "name" (name, name_m, name_f, name_mp y name_fp)
!! y los valores "madera" y "caoba" en el campo "adjectives", ParseNoun
!! aceptaría las cadenas "caja", "caja de madera", "caja de madera de caoba",
!! "madera", "caoba",... para referirse al objeto. Con la rutina modificada se
!! permite hacer parseado estricto de forma que las cadenas "madera" y "caoba"
!! no sean cadenas válidas. Es decir, de manera que sea precisa la existencia
!! de al menos una coincidencia con los valores de los campos "name" del
!! objeto.
!!
!!	@returns {integer} Número de palabras reconocidas en la entrada del usuario
!!	@override SpanishG.h
!!	@version 1.0
!!------------------------------------------------------------------------------
[ ParseNoun obj n dudas seguir gen p aux m nm nf nmp nfp a;

	n	= 0;	! núm. de palabras reconocidas de momento
	m	= 0;	! núm. de nombres reconocidos de momento
	nm	= 0;	! núm. de nombres masculinos reconocidos de momento
	nf	= 0;	! núm. de nombres femeninos reconocidos de momento
	nmp	= 0;	! núm. de nombres masculinos plurales reconocidos de momento
	nfp	= 0;	! núm. de nombres femeninos plurales reconocidos de momento
	a	= 0;	! núm. de adjetivos reconocidos de momento

	! Indica si hay que seguir mirando palabras para comprobar si las
	! siguientes se refieren o no a este mismo objeto
	seguir = 1;

	aux = aux;
	#ifdef DEBUG;
	aux = debug_flag;
	! Se desactiva el trazado de give
	! (para no cansar con tanto "nombreusado" cambiando continuamente)
	debug_flag = 0;
	#endif;
	give obj ~nombreusado; ! resetear nombreusado (quitar nombreusado)
	gen = 5;
	! 1=masculino,		  2=femenino,
	! 3=masculino plural, 4=femenino plural
	! 5=sin determinar

	while (seguir)
	{
		p = NextWord();
		if ((p=='de') && (n==0)) return 0;

		if (WordInProperty (p, obj, name_f))
		! Una palabra que se refiere al objeto
		{
			if (p) dict_flags_of_noun = (p->#dict_par1) & $$01110100;
			if (gen==5) gen = G_FEMENINO;
			n++;		! la contamos
			m++;
			nf++;
			n=n+dudas;	! añadimos todos los "de" "la"... que estaban sin contar
			dudas=0;	! y resetamos el contador de "de"s
!			print " --> es ahora femenino...." ; !infsp debug
			continue;	! volvemos al while
		}
		if (WordInProperty (p, obj, name_mp))
		! Una palabra que se refiere al objeto
		{
			if (p) dict_flags_of_noun = (p->#dict_par1) & $$01110100;
			if (gen==5) gen=G_MASCULINO+G_PLURAL;
			n++;		! la contamos
			m++;
			nmp++;
			n=n+dudas;	! añadimos todos los "de" "la"... que estaban sin contar
			dudas=0;	! y resetamos el contador de "de"s
			continue;	! volvemos al while
		}
		if (WordInProperty(p, obj, name_fp))
		! Una palabra que se refiere al objeto
		{
			if (p) dict_flags_of_noun = (p->#dict_par1) & $$01110100;
			if (gen==5) gen=G_FEMENINO+G_PLURAL;
			n++;		! la contamos
			m++;
			nfp++;
			n=n+dudas;	! añadimos todos los "de" "la"... que estaban sin contar
			dudas=0;	! y resetamos el contador de "de"s
			continue;	! volvemos al while
		}
		! [I7] se cambio de lugar este if para buscar el name_m (o name)
		! en ultimo lugar, debido a una interferencia q provoca la asignacion
		! 'automatica' de plural en los objetos I7
		if (WordInProperty(p, obj, name_m))
		! Una palabra que se refiere al objeto
		{
			if (p) dict_flags_of_noun = (p->#dict_par1) & $$01110100;
			if (gen==5) gen=G_MASCULINO;
			n++;		! la contamos
			m++;
			nm++;
			n=n+dudas;	! añadimos todos los "de" "la"... que estaban sin contar
			dudas=0;	! y resetamos el contador de "de"s
!			print " --> es ahora masculino...." ; !infsp debug
			continue;	! volvemos al while
		}
		if (WordInProperty(p, obj, adjectives))
		{
			if (p) dict_flags_of_noun = (p->#dict_par1) & $$01110100;
			n++;		! la contamos
			a++;
			n=n+dudas;	! añadimos todos los "de" "la"... que estaban sin contar
			dudas = 0;	! y resetamos el contador de "de"s
!			print (the) obj," --> adjetivo detectado....^" ; !infsp debug
			continue;	! volvemos al while
		}

		! Si la palabra no fue reconocida, miraremos si se trata
		! de 'de' o un artículo. En este caso mantenemos la duda y
		! seguimos mirando palabras, hasta que una de ellas encaje
		! en este objeto$ en cuyo caso se retornan todas las que se
		! han leido, o bien se encuentre una que no encaja en cuyo
		! caso se retorna las que se habían encontrado antes del "de"

		else if (p== 'el' or 'la' or 'los' or 'las' or 'de')
		{
			dudas++; continue;
		}
		! Si la palabra no fue reconocida, ni se trata de un artículo o de un
		! "de", se da la búsqueda por finalizada y se sale del bucle
		else seguir = 0;

	}! while block

	if (obj provides gender) {
		switch (gen) {
			1: give obj ~female ~pluralname;
			2: give obj female ~pluralname;
			3: give obj ~female pluralname;
			4: give obj female pluralname;
		}
	}

	! Si el matcheo fue por adjectives, entonces gen = 5 y "nombresusado" no
	! se han puesto
	if (gen < 5) give obj nombreusado;

	#ifdef DEBUG;
	debug_flag=aux;
	#endif;

	! En caso de que estemos usando parseado estricto, despreciamos el objeto
	! si no ha sido referenciado al menos por uno de sus nombres
	if (m == 0) return 0;

	return n; ! Se retorna número de palabras reconocidas
];


!!==============================================================================
!! Rutina para imprimir el nombre corto de un objeto. Además, crea un
!! hipervínculo relacionado con el objeto y, si el objeto tiene el atributo
!! 'remarkable' utiliza un estilo resaltado para imprimir el nombre.
!!
!!	@returns {boolean} Verdadero
!!	@override parserm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
[ PSN__ obj;
    if (obj == 0) { print (string) NOTHING__TX; rtrue; }
    switch (metaclass(obj)) {
	    Routine:  print "<routine ", obj, ">"; rtrue;
	    String:   print "<string ~", (string) obj, "~>"; rtrue;
	    nothing:  print "<illegal object number ", obj, ">"; rtrue;
    }
    if (obj == player) { print (string) YOURSELF__TX; rtrue; }
    #Ifdef LanguagePrintShortName;
    if (LanguagePrintShortName(obj)) rtrue;
    #Endif; ! LanguagePrintShortName
	return CreateLink(obj);
];


!!==============================================================================
!! Imprime el pronombre nominativo correcto correspondiente a un objeto.
!!
!!	@param {Object} obj - Obj. del que se desea imprimir su pronombre nominativo
!!	@version 1.0
!!------------------------------------------------------------------------------
[ SpanishNominativePronoun obj;
	switch (GetGNAOfObject(obj)) {
		0, 6:	print "él";
		1, 7:	print "ella";
		2, 8:	print "ello";
		3, 9:	print "ellos";
		4, 10:	print "ellas";
		5, 11:	print "ellos";
   }
];


!!==============================================================================
!! Créditos detallados de la aplicación.
!!
!!	@override verblibm.h
!!	@version 1.0
!!------------------------------------------------------------------------------
[ VersionSub ix;
	new_line;
	Banner();
	print "Versión de la librería Inform ", (string) LibRelease;
	print " / Núm. de serie librería ", (string) LibSerial;
	new_line;
	#Ifdef LanguageVersion;
	print (string) LanguageVersion, "^";
	#Endif; ! LanguageVersion
	#Ifdef TARGET_ZCODE;
	ix = 0; ! shut up compiler warning
	if (standard_interpreter > 0) {
		print "Estándar del intérprete ", standard_interpreter/256, ".",
			standard_interpreter%256, " (", HDR_TERPNUMBER->0;
		#Iftrue (#version_number == 6);
		print (char) '.', HDR_TERPVERSION->0;
		#Ifnot;
		print (char) HDR_TERPVERSION->0;
		#Endif;
		print ") / ";
	} else {
		print "Intérprete ", HDR_TERPNUMBER->0, " Versión ";
		#Iftrue (#version_number == 6);
		print HDR_TERPVERSION->0;
		#Ifnot;
		print (char) HDR_TERPVERSION->0;
		#Endif;
		print " / ";
	}
	#Ifnot; ! TARGET_GLULX;
	@gestalt 1 0 ix;
	print "Versión del intérprete ", ix / $10000, ".", (ix & $FF00) / $100,
	".", ix & $FF, " / ";
	@gestalt 0 0 ix;
	print "VM ", ix / $10000, ".", (ix & $FF00) / $100, ".", ix & $FF;
	#Endif; ! TARGET_;
	new_line;
];
