

!!==============================================================================
!!
!!	EL CHICO
!!	Definición de localidades y objetos de la casa
!!
!!==============================================================================
!!
!!	Archivo:		chico_objects.inf
!!	Autor(es):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Castellano)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		GLULX
!!	Versión:		0.0
!!	Fecha:			2015/01/29
!!
!!------------------------------------------------------------------------------
!!
!!	# HISTORIAL DE VERSIONES
!!
!!	0.0: 2015/01/29 Inicio del desarrollo.
!!
!!------------------------------------------------------------------------------
!!
!!	This program is free software; you can redistribute it and/or modify
!!	it under the terms of the GNU General Public License as published by
!!	the Free Software Foundation; either version 2 of the License, or
!!	(at your option) any later version.
!!
!!	This program is distributed in the hope that it will be useful,
!!	but WITHOUT ANY WARRANTY; without even the implied warranty of
!!	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!	GNU General Public License for more details.
!!
!!	You should have received a copy of the GNU General Public License along
!!	with this program; if not, write to the Free Software Foundation, Inc.,
!!	51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA. Or see
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (C) 2015, J. Francisco Martín
!!
!!------------------------------------------------------------------------------
!!
!!	# TABLA DE CONTENIDOS:
!!
!!	1)	Limbo
!!	2)	Descampado 1
!!	3)	Descampado 2
!!	4)	Terraza
!!	5)	Habitación
!!	6)	Pasillo
!!	7)	Baño
!!	8)	Salón
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	1)	Limbo
!!------------------------------------------------------------------------------

Room	LIMBO "en algún lugar"
 with	name_f	'babia',
		name_m	'limbo' 'lugar' 'sueno' 'vacio',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		clarification 0,
 has	male;

Atrezzo	cielo_nocturno "cielo"
 with	found_in DESCAMPADO_1 DESCAMPADO_2,
		name_f	'contaminacion' 'estrella' 'luna' 'noche',
		name_m	'astro' 'cielo' 'firmamento',
		name_fp	'estrellas',
		gender	G_MASCULINO,
		adjectives
			'anaranjada' 'anaranjado' 'cerrada' 'cerrado' 'encapotada' 
			'encapotado',
		description [; return O__M(self, 1); ],
		react_before [;
			Examine, Search:
				if (noun == u_obj)
					<<Examine self>>;
		],
 has	male;

Atrezzo	cielo_diurno "cielo"
 with	found_in TERRAZA,
		name_f	'atmosfera',
		name_m	'astro' 'cielo' 'dia' 'firmamento' 'sol',
		gender	G_MASCULINO,
		adjectives 'azul' 'brillante',
		description [; return O__M(self, 1); ],
		react_before [;
			Examine, Search:
				if (noun == u_obj)
					<<Examine self>>;
		],
 has	male;


!!==============================================================================
!!	2)	Descampado 1
!!------------------------------------------------------------------------------

Outdoors	DESCAMPADO_1 "Descampado"
 with	name_m	'descampado',
		gender	G_MASCULINO,
		initial [; if (self hasnt visited) {
			!! Establece la flexión gramatical de la librería:
			SetGrammaticalInflection(FIRST_PERSON_PRESENT);
			!! Texto inicial de la Primera Parte:
			new_line;
			HorizontalRule();
			new_line;
			O__M(self, 1);
			new_line;
			return true;
		}],
		description [; 
			if (self hasnt visited) return O__M(self, 2);
			return O__M(self, 3);
		],
		before [;
			Smell:
				self.smelled = true;
				return O__M(self, 4);
		],
		!! Indica si se ha introducido la acción ##Smell en la localidad:
		smelled false,
 has	male;

!! XXX OBJETO PROTAGONISTA
Object	-> Chico "chico"
 with	name_m	'personaje' 'protagonista',
		gender	G_MASCULINO,
		adjectives 'principal',
		description [; return O__M(self, 1); ],
 has	animate male proper;

Atrezzo	-> coche "coche"
 with	name_m	'audi' 'coche' 'vehiculo',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	male;

Atrezzo	-> bolsas "bolsas de basura"
 with	name_fp	'bolsas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives	'basura',
 has	female pluralname;

Furniture	-> cadaver_Yamileth "cadáver"
 with	name_f	'mujer' 'persona' 'yamileth',
		name_m	'bulto' 'cadaver' 'cuerpo',
		gender	G_FEMENINO,
		description [;
			return O__M(self, 1);
		],
		before [;
			Cut:
				if (second == nothing or sierra
					&& IndirectlyContains(player, sierra)) {
					O__M(self, 2);
					new_line;
					if (DESCAMPADO_1.smelled) { O__M(self, 3); new_line; }
					PressAnyKey();
					!! XXX - Fin de la Primera Parte
					PlayerTo(TERRAZA);
					return true;
				}
			DefaultAction:
				if (self has described && IndirectlyContains(player, sierra))
					<<Cut self>>;
		],
 has	female;

Item	-> sierra "sierra"
 with	name_f	'sierra',
		gender	G_FEMENINO,
		description [;
			if (self hasnt described) return O__M(self, 1);
			return O__M(self, 2);
		],
		before [;
			DefaultAction:
				if (self has described && ~~IndirectlyContains(player, self))
					<<Take self>>;
		],
		after [; Take: return O__M(self, 3); ],
 has	concealed female;

NPC		-> Mario "Mario"
 with	name_f	'persona',
		name_m	'hombre' 'mario',
		gender	G_MASCULINO,
		description [; 
			O__M(self, 1);
			if (self hasnt described) O__M(self, 2);
			new_line;
			return true;
		],
		before [;
			TalkTo:
				ConversationManager.start(conversacion_Mario_1);
				return true;
		],
		gets_angry [ i;
			if (i == 0) i = 1;
			self.angry = self.angry + i;
			switch (self.angry) {
				1:	O__M(self, 3);
				2:	O__M(self, 4);
				3:	O__M(self, 5);
			}
		],
 private
		!! Controla el enfado de Mario con el jugador:
		angry 0,
 has	concealed male proper;

NPC		-> Alberto "Alberto"
 with	name_f	'persona',
		name_m	'hombre' 'alberto',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
 has	concealed male proper;

Conversation	conversacion_Mario_1 "(conversación con Mario)"
 with	initial_action [;
			self.add_topic(cm1_op01);
			self.add_topic(cm1_op02);
			self.add_topic(cm1_op03);
			return O__M(self, 1);
		],
		inter_action [;
			"Inter action.";
		],
		final_action [;
			"Final action.";
		];

ConversationTopic	cm1_op01
 with	entry [; CreateLink("No puedo hacerlo"); ],
		keys 'capaz' 'hacer' 'hacerlo' 'no' 'puedo' 'soy',
		reply [; O__M(self, 1); ],
		reaction [;
			new_line;
			Mario.gets_angry();
			if (~~conversacion_Mario_1.has_topic(cm1_op02)) {
				give cm1_op03 ~visited;
				conversacion_Mario_1.add_topic(cm1_op03);
				conversacion_Mario_1.add_topic(cm1_op06);
				conversacion_Mario_1.remove_topic(cm1_op04);
			}
		],
		relatives	cm1_op03,
		subtopics	cm1_op04 cm1_op05,
		append_topic_inventory true;

ConversationTopic	cm1_op02
 with	entry [; CreateLink("No pienso hacerlo"); ],
		keys 'hacer' 'hacerlo' 'no' 'pienso' 'quiero',
		reply [; return O__M(self, 1); ],
		reaction [;
			new_line;
			Mario.gets_angry();
			if (~~conversacion_Mario_1.has_topic(cm1_op01)) {
				give cm1_op03 ~visited;
				conversacion_Mario_1.add_topic(cm1_op03);
				conversacion_Mario_1.add_topic(cm1_op06);
				conversacion_Mario_1.remove_topic(cm1_op04);
			}
		],
		relatives	cm1_op03,
		subtopics	cm1_op04 cm1_op05,
		append_topic_inventory true;

ConversationTopic	cm1_op03
 with	entry [; CreateLink("Vale"); ],
		keys 'acuerdo' 'vale',
		reply [; return O__M(self, 1); ],
		append_topic_inventory false;

ConversationTopic	cm1_op04
 with	entry [; CreateLink("Nada"); ],
		keys 'nada',
		reply [; return O__M(self, 1); ],
		append_topic_inventory false;

ConversationTopic	cm1_op05
 with	entry [;
			KeywordStyle(3);
			CreateLink("Guardar silencio", false, true);
			RomanStyle();
		],
		keys 'guarda' 'guardar' 'silencio',
		reply [; return O__M(self, 1); ],
		append_topic_inventory false;

ConversationTopic	cm1_op06
 with	entry [; CreateLink("Que te jodan"); ],
		keys 'follen' 'jodan' 'jodete',
		reply [; return O__M(self, 1); ],
		reaction [; new_line; Mario.gets_angry(); ],
		relatives	cm1_op03 cm1_op05,
		subtopics	cm1_op07 cm1_op08,
		append_topic_inventory true;

ConversationTopic	cm1_op07
 with	entry [; CreateLink("Hijo de puta"); ],
		keys 'hijo' 'hijoputa' 'puta',
		reply [; return O__M(self, 1); ],
		reaction [;
			new_line;
			PressAnyKey();
			HorizontalRule();
			new_line;
			PressAnyKey();
			Banner();
			new_line;
			PressAnyKey();
			StartParserStyle();
			CreateLink("Haz click aquí o pulsa una tecla para acabar");
			EndParserStyle();
			PressAnyKey();
			@restart;
			L__M(##Restart, 2);
			return true;
		],
		append_topic_inventory false,
		final_topic true;

ConversationTopic	cm1_op08
 with	entry [; CreateLink("Perdón"); ],
		keys 'perdon' 'siento',
		reply [; return O__M(self, 1); ],
		final_topic true;


!!==============================================================================
!!	3)	Descampado 2
!!------------------------------------------------------------------------------

Outdoors	DESCAMPADO_2 "Descampado"
 with	name_m	'descampado',
		gender	G_MASCULINO,
		initial [; if (self hasnt visited) {
			!! Establece la flexión gramatical:
			SetGrammaticalInflection(FIRST_PERSON_PRESENT);
			return true;
		}],
 has	male;

!!==============================================================================
!!	4)	Terraza
!!------------------------------------------------------------------------------

Outdoors	TERRAZA "Terraza"
 with	name_f	'terraza',
		name_m	'patio',
		gender	G_FEMENINO,
		initial [; if (self hasnt visited) {
			!! Establece la flexión gramatical:
			SetGrammaticalInflection(FIRST_PERSON_PAST);
			!! Texto inicial de la escena:
			O__M(self, 1);
			new_line;
			return true;
		}],
		description [;
			return O__M(self, 2);
		],
		w_to	HABITACION,
		in_to [; return self.w_to(); ],
 has	female;


!!==============================================================================
!!	5)	Habitación
!!------------------------------------------------------------------------------

Indoors	HABITACION "Pequeña habitación"
 with	name_f	'habitacion',
		name_m	'cuarto',
		adjectives	'pequena' 'pequeno',
		gender	G_FEMENINO,
		description [;
			O__M(self, 1);
			if (self hasnt visited) O__M(self, 2);
			new_line;
			return true;
		],
		n_to	PASILLO,
		e_to	TERRAZA,
		w_to	SALON,
		out_to [; return self.e_to(); ],
 has	female;


!!==============================================================================
!!	6)	Pasillo
!!------------------------------------------------------------------------------

Indoors	PASILLO "Pasillo"
 with	name_m	'corredor' 'pasillo',
		gender	G_MASCULINO,
		description [;
			return O__M(self, 1);
		],
		s_to	HABITACION,
		e_to	BANNO,
 has	male;


!!==============================================================================
!!	7)	Baño
!!------------------------------------------------------------------------------

Indoors	BANNO "Baño"
 with	name_m	'aseo' 'bano',
		gender	G_MASCULINO,
		description [;
			return O__M(self, 1);
		],
		w_to	PASILLO,
 has	male;

!!==============================================================================
!!	8)	Salón
!!------------------------------------------------------------------------------

Indoors	SALON "Salón"
 with	name_f	'estancia' 'habitacion' 'sala',
		name_m	'cuarto' 'salon',
		gender	G_MASCULINO,
		description [;
			return O__M(self, 1);
		],
		e_to	HABITACION,
 has	male;


