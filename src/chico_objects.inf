

!!==============================================================================
!!
!!	EL CHICO
!!	Definición de localidades y objetos de la casa
!!
!!==============================================================================
!!
!!	Archivo:		chico_objects.inf
!!	Autor(es):		J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Castellano)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		GLULX
!!	Versión:		B1.0
!!	Fecha:			2015/01/29
!!
!!------------------------------------------------------------------------------
!!
!!	# HISTORIAL DE VERSIONES
!!
!!	B1.0: 2015/02/10 Versión BETA de la primera parte.
!!	0.0: 2015/01/29 Inicio del desarrollo.
!!
!!------------------------------------------------------------------------------
!!
!!	This program is free software; you can redistribute it and/or modify
!!	it under the terms of the GNU General Public License as published by
!!	the Free Software Foundation; either version 2 of the License, or
!!	(at your option) any later version.
!!
!!	This program is distributed in the hope that it will be useful,
!!	but WITHOUT ANY WARRANTY; without even the implied warranty of
!!	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!!	GNU General Public License for more details.
!!
!!	You should have received a copy of the GNU General Public License along
!!	with this program; if not, write to the Free Software Foundation, Inc.,
!!	51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA. Or see
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (C) 2015, J. Francisco Martín
!!
!!------------------------------------------------------------------------------
!!
!!	# TABLA DE CONTENIDOS:
!!
!!	1)	Limbo
!!	2)	Descampado 1
!!	3)	Descampado 2
!!	4)	Terraza
!!	5)	Habitación
!!	6)	Pasillo
!!	7)	Baño
!!	8)	Salón
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!	1)	Limbo
!!------------------------------------------------------------------------------

Room	LIMBO "en algún lugar"
 with	name_f	'babia',
		name_m	'limbo' 'lugar' 'sueno' 'vacio',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		clarification 0,
 has	male;

Atrezzo	cielo_nocturno "cielo"
 with	found_in DESCAMPADO_1 DESCAMPADO_2,
		name_f	'contaminacion' 'estrella' 'luna' 'noche',
		name_m	'astro' 'cielo' 'firmamento',
		name_fp	'estrellas',
		gender	G_MASCULINO,
		adjectives
			'anaranjada' 'anaranjado' 'cerrada' 'cerrado' 'encapotada' 
			'encapotado',
		description [; return O__M(self, 1); ],
		react_before [;
			Examine, Search:
				if (noun == u_obj)
					<<Examine self>>;
		],
 has	male;

Atrezzo	cielo_diurno "cielo"
 with	found_in TERRAZA,
		name_f	'atmosfera',
		name_m	'astro' 'cielo' 'dia' 'firmamento' 'sol',
		gender	G_MASCULINO,
		adjectives 'azul' 'brillante',
		description [; return O__M(self, 1); ],
		react_before [;
			Examine, Search:
				if (noun == u_obj)
					<<Examine self>>;
		],
 has	male;

Conversation	conversacion_Mario_1 "(conversación con Mario)"
 with	initial_action [;
			self.add_topic(cm1_op01);
			self.add_topic(cm1_op02);
			self.add_topic(guardar_silencio);
			return O__M(self, 1);
		],
		inter_action [;
			switch (Mario.anger) {
				0, 3:
					self.add_topic(guardar_silencio);
					return O__M(self, 2);
				1:
					return O__M(self, 3);
				2:
					self.add_topic(guardar_silencio);
					return O__M(self, 4);
				default:
					return O__M(self, 5);
			}
		],
		final_action [; return O__M(self, 2); ];

ConversationTopic	guardar_silencio
 with	entry [ st;
			st = _current_style;
			KeywordStyle(3);
			CreateLink("Guardar silencio", false, true);
			switch (st) {
				_IST_:		ItalicStyle();
				_BST_:		BoldStyle();
				_MST_:		MonospacedStyle();
				default:	RomanStyle();
			}
		],
		keys 'guarda' 'guardar' 'silencio',
		reply [; "..."; ],
		!! El controlador de conversaciones trata el tema y lo elimina de la 
		!! conversación. Al quitarle aquí la propiedad 'visited' podemos 
		!! reutilizarlo y añadirlo más adelante a la misma conversación o a 
		!! otra conversación diferente:
		reaction [; give self ~visited; ],
		append_topic_inventory false;

ConversationTopic	cm1_op01
 with	entry [; CreateLink("No puedo hacerlo"); ],
		keys 'capaz' 'hacer' 'hacerlo' 'no' 'puedo' 'soy',
		reply [; return O__M(self, 1); ],
		reaction [;
			new_line;
			Mario.gets_angry();
			if (Mario.anger == 1) {
				!! [No pienso hacerlo / ¿Estás sordo? / Nada]
				conversacion_Mario_1.add_topic(cm1_op03);
				conversacion_Mario_1.add_topic(cm1_op04);
				conversacion_Mario_1.remove_topic(guardar_silencio);
			}
			else {
				!! [Hijo de puta / Guardar silencio]
				conversacion_Mario_1.add_topic(cm1_op05);
				conversacion_Mario_1.add_topic(guardar_silencio);
				conversacion_Mario_1.remove_topic(cm1_op03);
				conversacion_Mario_1.remove_topic(cm1_op04);
			}
		],
		append_topic_inventory true;

ConversationTopic	cm1_op02
 with	entry [; CreateLink("No pienso hacerlo"); ],
		keys 'hacer' 'hacerlo' 'no' 'pienso' 'quiero',
		reply [; return O__M(self, 1); ],
		reaction [;
			new_line;
			Mario.gets_angry();
			if (Mario.anger == 1) {
				!! [No puedo hacerlo / ¿Estás sordo? / Nada]
				conversacion_Mario_1.add_topic(cm1_op03);
				conversacion_Mario_1.add_topic(cm1_op04);
				conversacion_Mario_1.remove_topic(guardar_silencio);
			}
			else {
				!! [Hijo de puta / Guardar silencio]
				conversacion_Mario_1.add_topic(cm1_op05);
				conversacion_Mario_1.add_topic(guardar_silencio);
				conversacion_Mario_1.remove_topic(cm1_op03);
				conversacion_Mario_1.remove_topic(cm1_op04);
			}
		],
		append_topic_inventory true;

ConversationTopic	cm1_op03
 with	entry [; CreateLink("¿Estás sordo?"); ],
		keys '¿estas' 'estas' 'sordo' 'sordo?' 'subnormal',
		reply [; return O__M(self, 1); ],
		reaction [; Mario.gets_angry(4); ],
		relatives	cm1_op01 cm1_op02 cm1_op04,
		subtopics	cm1_op06 cm1_op07,
		append_topic_inventory true;

ConversationTopic	cm1_op04
 with	entry [; CreateLink("Nada"); ],
		keys 'importa' 'nada' 'no',
		reply [; return O__M(self, 1); ],
		reaction [; Mario.anger = 3; ],
		relatives	cm1_op01 cm1_op02 cm1_op03,
		subtopics	cm1_op05 guardar_silencio,
		append_topic_inventory false;

ConversationTopic	cm1_op05
 with	entry [; CreateLink("Hijo de puta"); ],
		keys 'hijo' 'hijoputa' 'puta',
		reply [; return O__M(self, 1); ],
		reaction [; Mario.gets_angry(5); ],
		relatives	guardar_silencio,
		subtopics	cm1_op06 cm1_op07,
		append_topic_inventory true;

ConversationTopic	cm1_op06
 with	entry [; CreateLink("Perdón"); ],
		keys 'perdon' 'siento',
		reply [; return O__M(self, 1); ],
		reaction [;
			give TERRAZA general;
			return DESCAMPADO_1.end_scene();
		],
		relatives	cm1_op07,
		append_topic_inventory false,
		final_topic true;

ConversationTopic	cm1_op07
 with	entry [; CreateLink("Que te jodan"); ],
		keys 'follen' 'jodan' 'jodete',
		reply [; return O__M(self, 1); ],
		reaction [;
			return DESCAMPADO_1.end_scene();
		],
		append_topic_inventory false,
		final_topic true;


!!==============================================================================
!!	2)	Descampado 1
!!------------------------------------------------------------------------------

Outdoors	DESCAMPADO_1 "Descampado"
 with	name_f	'llanura' 'planicie',
		name_m	'descampado' 'paramo',
		gender	G_MASCULINO,
		initial [; if (self hasnt visited) {
			!! Establece la flexión gramatical de la librería:
			SetGrammaticalInflection(FIRST_PERSON_PRESENT);
			!! Texto inicial de la Primera Parte:
			new_line;
			HorizontalRule();
			new_line;
			O__M(self, 1);
			new_line;
			return true;
		}],
		description [; 
			if (self hasnt visited) return O__M(self, 2);
			return O__M(self, 3);
		],
		before [;
			Jump:
				return O__M(self, 6);
			Listen:
				return O__M(self, 5);
			Smell:
				self.smelled = true;
				return O__M(self, 4);
		],
		!! Indica si se ha introducido la acción ##Smell en la localidad:
		smelled false,
		end_scene [;
			PressAnyKey();

			#Ifdef PART_ONE;
			new_line;
			HorizontalRule();
			new_line;
			PressAnyKey();
			print (strong) "EL CHICO - Fin de la Primera Parte^";
			print "Por J. Francisco Martín^";
			new_line;
			PressAnyKey();
			!! Escribe las decisiones en un archivo externo y reinicia:
			StartParserStyle();
			CreateLink("Haz click aquí o pulsa una tecla para acabar");
			EndParserStyle();
			WriteDecisionsFile();
			PressAnyKey();
			@restart;
			L__M(##Restart, 2);

			#Ifnot;
			PlayerTo(TERRAZA);
			return true;
			#Endif; ! BETA_RELEASE;
		],
 has	male;

#Ifdef PART_ONE;
!! XXX OBJETO PROTAGONISTA
Object	-> Chico "chico"
 with	name_m	'personaje' 'protagonista',
		gender	G_MASCULINO,
		adjectives 'principal',
		description [; return O__M(self, 1); ],
 has	animate male proper;
#Endif; ! PART_ONE;

#Ifdef COMPLETE_VERSION;
!! XXX OBJETO PROTAGONISTA
Object	-> Chico "chico"
 with	name_m	'personaje' 'protagonista',
		gender	G_MASCULINO,
		adjectives 'principal',
		description [; return O__M(self, 1); ],
 has	animate male proper;
#Endif; ! COMPLETE_VERSION;

Atrezzo	-> "faros"
 with	name_f	'lampara' 'linterna' 'luz',
		name_fp	'lamparas' 'linternas' 'luces',
		name_m	'faro' 'foco',
		name_mp	'faros' 'focos',
		gender	G_MASCULINO + G_PLURAL,
		adjectives
			'encendida' 'encendidas' 'encendido' 'encendidos' 
			'puesta' 'puestas' 'puesto' 'puestos',
		before [;
			Examine, Look:
				<<Examine coche>>;
		],
 has	male pluralname;

Atrezzo	-> "cerebro"
 with	name_f	'cabellera' 'melena' 'sesera',
		name_fp	'seseras',
		name_m	'cabello' 'cerebro' 'pelo' 'seso',
		name_mp	'cabellos' 'pelos' 'sesos',
		gender	G_MASCULINO,
		adjectives
			'enrredada' 'enrredadas' 'enrredado' 'enrredados' 'ensangrentada' 
			'ensangrentadas' 'ensangrentado' 'ensangrentados' 'negra' 'negro' 
			'negros',
		before [;
			Examine, Look:
				<<Examine cadaver_Yamileth>>;
		],
 has	male;

Atrezzo	-> "pistola"
 with	name_f	'arma' 'pistola',
		gender	G_FEMENINO,
		adjectives 'negra',
		before [;
			Examine, Look:
				<<Examine Mario>>;
		],
 has	female;

Atrezzo	-> "cigarrillo"
 with	name_m	'cigarrillo' 'cigarro' 'pitillo' 'tabaco',
		gender	G_MASCULINO,
		adjectives 'ardiente' 'encendido' 'incandescente',
		before [;
			Examine, Look:
				<<Examine Alberto>>;
		],
 has	male;

Atrezzo	-> coche "coche"
 with	name_m	'audi' 'auto' 'automovil' 'capo' 'carro' 'coche' 'vehiculo',
		gender	G_MASCULINO,
		adjectives 'negro',
		description [; return O__M(self, 1); ],
 has	male;

Atrezzo	-> bolsas "bolsas de basura"
 with	name_f	'bolsa',
		name_fp	'bolsas',
		name_m	'saco',
		name_mp	'sacos',
		gender	G_FEMENINO + G_PLURAL,
		adjectives 'basura' 'negra' 'negras' 'negro' 'negros',
		description [; return O__M(self, 1); ],
 has	female pluralname;

Furniture	-> cadaver_Yamileth "cadáver"
 with	name_f	'chica' 'dama' 'difunta' 'gente' 'joven' 'machupichu' 'moza' 
				'muchacha' 'muerta' 'mujer' 'persona' 'senora' 'senorita' 
				'tiraflechas' 'yamileth',
		name_m	'bulto' 'cadaver' 'cuerpo' 'fiambre' 'npc' 'personaje' 'pnj',
		gender	G_FEMENINO,
		description [; return O__M(self, 1); ],
		before [;
			Cut:
				if (second == nothing or sierra
					&& IndirectlyContains(player, sierra)) {
					O__M(self, 2);
					if (DESCAMPADO_1.smelled) { new_line; O__M(self, 3); }
					DESCAMPADO_1.end_scene();
					return true;
				}
				return O__M(self, 4);
			DefaultAction:
				if (self has described && IndirectlyContains(player, sierra))
					<<Cut self>>;
		],
 has	female;

Item	-> sierra "sierra"
 with	name_f	'segueta' 'serreta' 'sierra',
		name_m	'serrucho',
		gender	G_FEMENINO,
		description [;
			if (self hasnt described) return O__M(self, 1);
			return O__M(self, 2);
		],
		before [;
			DefaultAction:
				if (self has described && ~~IndirectlyContains(player, self))
					<<Take self>>;
		],
		after [; Take: return O__M(self, 3); ],
 has	concealed female;

NPC		-> Mario "Mario"
 with	name_f	'gente' 'persona',
		name_m	'hombre' 'mario' 'npc' 'personaje' 'pnj',
		gender	G_MASCULINO,
		description [; 
			O__M(self, 1);
			if (self hasnt described) O__M(self, 2);
			new_line;
			return true;
		],
		react_before [;
			NPCTalk, TalkTo:
				return false;
			default:
				if (self.anger > 3)
					<<TalkTo self>>;
		],
		before [;
			Attack:
				return O__M(self, 6);
			DefaultAction:
				if (self.talked) <<TalkTo self>>;
			TalkTo:
				self.talked = true;
				ConversationManager.start(conversacion_Mario_1);
				return true;
		],
		!! La reacciones de Mario variarán en función del enfado del personaje, 
		!! que se registra a través del atributo 'anger'. Esta función se 
		!! encarga de modificar el valor del enfado. Acepta un parámetro 
		!! opcional. Si no se indica ninguno, el enfado de Mario se incrementa 
		!! en una unidad. Si se indica, el estado de enfado toma el valor 
		!! pasado como parámetro. Los valores de enfado posibles son:
		!!
		!!	0)	Estado inicial.
		!!	1)	El chico ha dicho que no puede/quiere desmembrar el cadáver.
		!!	2)	El chico ha repetido que no puede/quiere desmembrar el cadáver.
		!!	3)	El chico no ha seguido protestando después del estado #1.
		!!	4)	El chico le ha preguntado a Mario si estaba sordo.
		!!	5)	El chico ha llamado hijo de puta a Mario.
		gets_angry [ arg;
			if (arg == 0) self.anger = self.anger + 1;
			else self.anger = arg;
			switch (self.anger) {
				1:	O__M(self, 3);
				2:	O__M(self, 4);
				default: O__M(self, 5);
			}
		],
		!! Registra el estado del enfado de Mario con el chico:
		anger 0,
		!! Indica si el jugador a hablado o no con Mario:
		talked false,
 has	concealed male proper;

NPC		-> Alberto "Alberto"
 with	name_f	'gente' 'persona',
		name_m	'alberto' 'berto' 'hombre' 'npc' 'personaje' 'pnj',
		gender	G_MASCULINO,
		description [; return O__M(self, 1); ],
		before [;
			Attack:
				return O__M(self, 3);
			TalkTo:
				return O__M(self, 2);
		],
 has	concealed male proper;


!!==============================================================================
!!	3)	Descampado 2
!!------------------------------------------------------------------------------

Outdoors	DESCAMPADO_2 "Descampado"
 with	name_m	'descampado',
		gender	G_MASCULINO,
		initial [; if (self hasnt visited) {
			!! Establece la flexión gramatical:
			SetGrammaticalInflection(FIRST_PERSON_PRESENT);
			return true;
		}],
 has	male;

!!==============================================================================
!!	4)	Terraza
!!------------------------------------------------------------------------------

Outdoors	TERRAZA "Terraza"
 with	name_f	'terraza',
		name_m	'patio',
		gender	G_FEMENINO,
		initial [; if (self hasnt visited) {
			!! Establece la flexión gramatical:
			SetGrammaticalInflection(FIRST_PERSON_PAST);
			!! Escribe el texto inicial de la Segunda Parte:
			new_line;
			HorizontalRule();
			new_line;
			if (self hasnt general) O__M(self, 1);
			else O__M(self, 2);
			new_line;
			return true;
		}],
		description [;
			return O__M(self, 3);
		],
		w_to	SALA_LECTURA,
		in_to [; return self.w_to(); ],
 has	female;

#Ifdef PART_TWO;
!! XXX OBJETO PROTAGONISTA
Object	-> Chico "chico"
 with	name_m	'personaje' 'protagonista',
		gender	G_MASCULINO,
		adjectives 'principal',
		description [; return O__M(self, 1); ],
 has	animate male proper;
#Endif; ! PART_TWO;

Furniture	-> puertas_cristal "puertas de cristal"
 with	name_f	'entrada' 'puerta' 'salida',
		name_fp	'puertas',
		gender	G_FEMENINO + G_PLURAL,
		adjectives 'cristal',
 has	female pluralname;


!!==============================================================================
!!	5)	Sala de lectura
!!------------------------------------------------------------------------------

Indoors	SALA_LECTURA "Sala de lectura"
 with	name_f	'habitacion' 'sala',
		name_m	'cuarto',
		adjectives	'lectura' 'pequena' 'pequeno',
		gender	G_FEMENINO,
		description [;
			O__M(self, 1);
			if (self hasnt visited) O__M(self, 2);
			new_line;
			return true;
		],
		n_to	PASILLO,
		e_to	TERRAZA,
		w_to	SALON,
		out_to [; return self.e_to(); ],
 has	female;


!!==============================================================================
!!	6)	Pasillo
!!------------------------------------------------------------------------------

Indoors	PASILLO "Pasillo"
 with	name_m	'corredor' 'pasillo',
		gender	G_MASCULINO,
		description [;
			return O__M(self, 1);
		],
		s_to	SALA_LECTURA,
		e_to	BANNO,
 has	male;

Furniture	-> puerta_banno "puerta del baño"
 with	name_m	'bano',
		name_f	'puerta',
		gender	G_FEMENINO,
		adjectives 'abierta' 'cerrada',
 has	female;

Furniture	-> puerta_despacho "puerta del despacho"
 with	name_m	'despacho',
		name_f	'puerta',
		gender	G_FEMENINO,
		adjectives 'abierta' 'cerrada',
 has	female;


!!==============================================================================
!!	7)	Baño
!!------------------------------------------------------------------------------

Indoors	BANNO "Baño"
 with	name_m	'aseo' 'bano',
		gender	G_MASCULINO,
		description [;
			return O__M(self, 1);
		],
		w_to	PASILLO,
 has	male;

!!==============================================================================
!!	8)	Salón
!!------------------------------------------------------------------------------

Indoors	SALON "Salón"
 with	name_f	'estancia' 'habitacion' 'sala',
		name_m	'cuarto' 'salon',
		gender	G_MASCULINO,
		description [;
			return O__M(self, 1);
		],
		e_to	SALA_LECTURA,
 has	male;


